plugins {
    id 'java'
    id "com.teamscale" version "35.0.0"
}

import com.teamscale.reporting.testwise.TestwiseCoverageReport

teamscale {
    server {
        url = 'http://localhost:8080/'
        userName = 'admin'
        userAccessToken = 'q4tu9vfAAjQZ1peCpPvQHSrLi5CeIcGY'
        project = 'tia-junit5'
    }
}

tasks.register('testwiseCoverageReport', TestwiseCoverageReport) {
    executionData(tasks.test)
}

tasks.test {
    useJUnitPlatform()
    finalizedBy(tasks.testwiseCoverageReport)
    jacoco {
        includes = ["tia.*"]
    }
    teamscale {
        collectTestwiseCoverage = true
    }
}

group = 'eu.cqse.tutorials'
version = '1.0-SNAPSHOT'

// Please make use of this toolchain feature also in
// your Gradle projects to specify the Java version
// to use. Setting paths to particular JDK installations
// has shown to be error prone and is not supported by
// some Gradle plugins well.
java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(21))
  }
}

dependencies {
    testImplementation(platform('org.junit:junit-bom:5.12.0'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = "short"
        showCauses = false
        events "failed", "passed"
    }
}

