plugins {
    id 'java'
    id "com.teamscale" version "34.0.1"
}

teamscale {

    server {
        url = 'http://localhost:8080/'
        userName = 'admin'
        userAccessToken = 'q4tu9vfAAjQZ1peCpPvQHSrLi5CeIcGY'
        project = 'tia-junit5'
    }

    report {
        testwiseCoverage { partition = 'Unit Tests' }
    }

}

tasks.register('tiaTests', com.teamscale.TestImpacted) {
    useJUnitPlatform()
    testClassesDirs = testing.suites.test.sources.output.classesDirs
    classpath = testing.suites.test.sources.runtimeClasspath
    jacoco {
        includes = ["tia.*"]
    }
}

group 'eu.cqse.tutorials'
version '1.0-SNAPSHOT'

// Please make use of this toolchain feature also in
// your Gradle projects to specify the Java version 
// to use. Setting paths to particular JDK installations
// has shown to be error prone and is not supported by
// some Gradle plugins well.
java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(21))
  }
}

dependencies {
    testImplementation(platform('org.junit:junit-bom:5.10.3'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        exceptionFormat "short"
        showCauses false
        events "failed", "passed"
    }
}

